def scalaVersionMajor ='2.12'
def scalaVersionMinor ='2.12.8'
def akkaVersion = '2.6.5'
def akkaJdbcPersistenceJournalVersion = '3.5.3'
def h2DatabaseLibraryVersion = '1.4.200'
def slickVersion = '3.3.2'
def logbackVersion = '1.2.3'

// -------- DEPENDENCIES --------
// Sample dependencies
// def playJsonDep = "com.typesafe.play:play-json_$scalaVersion:2.6.9".toString()
// def jodaDep = 'org.joda:joda-convert:2.1'
// ------------------------------

// for scalaTest
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.26"
    }
}

// define common properties/behaviour
//  (assume all sub projects are scala)
subprojects {
    apply plugin: 'scala'
    apply plugin: 'application'
    apply plugin: "com.github.maiflai.scalatest" // for scalaTest

    group 'file-ingest-service-poc'
    version '0.1'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.scala-lang:scala-library:$scalaVersionMinor".toString()
        // compile 'log4j:log4j:1.2.17'

        testCompile "org.scalatest:scalatest_$scalaVersionMajor:3.1.1".toString()
    }

    // copy dependencies to build/dependencies
    task copyDeps(type: Copy) {
        from configurations.runtime
        into 'build/dependencies'
    }

    // run copyDeps before jar...
    jar.dependsOn copyDeps
}

// project specific
project(':web-server') {
    mainClassName = "bmaso.file_ingest_service_poc.web_server.MainCLI"

    dependencies {
    }

    jar {
        manifest {
            attributes 'Main-Class': 'bmaso.file_ingest_service_poc.web_server.MainCLI'
        }
    }
}

project(':cluster-node') {
    mainClassName = "bmaso.file_ingest_service_poc.cluster_node.MainCLI"

    dependencies {
        compile project(':message-protocol')
        compile group: 'com.typesafe.akka', name: "akka-persistence-typed_${scalaVersionMajor}", version: akkaVersion
        compile group: 'com.typesafe.akka', name: "akka-persistence-query_${scalaVersionMajor}", version: akkaVersion
        compile group: 'com.typesafe.akka', name: "akka-cluster-typed_${scalaVersionMajor}", version: akkaVersion
        compile group: 'com.typesafe.akka', name: "akka-cluster-sharding-typed_${scalaVersionMajor}", version: akkaVersion

        // Akka Persistence Journal & Persistence Query JDBC plugin
        compile group: 'com.github.dnvriend', name: "akka-persistence-jdbc_${scalaVersionMajor}", version: akkaJdbcPersistenceJournalVersion

        // H2 JDBC driver used in integration and CLI smoke tests
        testCompile group: 'com.h2database', name: 'h2', version: h2DatabaseLibraryVersion
        testCompile group: 'com.typesafe.slick', name: "slick_${scalaVersionMajor}", version: slickVersion

        // Logging in test CLIs and unit tests is done through logback
        testCompile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    }

    jar {
        manifest {
            attributes 'Main-Class': 'bmaso.file_ingest_service_poc.cluster_node.MainCLI'
        }
    }
}

project(':message-protocol') {
    mainClassName = "bmaso.file_ingest_service_poc.web_server.MainCLI"

    dependencies {
      compile "org.typelevel:cats_${scalaVersionMajor}:0.9.0"
      compile "com.typesafe.akka:akka-actor-typed_${scalaVersionMajor}:2.6.5"
    }

    jar {
        manifest {
            attributes 'Main-Class': 'bmaso.file_ingest_service_poc.web_server.MainCLI'
        }
    }
}
